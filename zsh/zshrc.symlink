#!/usr/bin/env zsh
################################################################################
#
#                                    zshrc                                     #
################################################################################


# your project folder that we can `c [tab]` to
# export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
[[ -a ~/.localrc ]] && source ~/.localrc

fpath=($fpath "$DOTFILES/functions" "$DOTFILES/private/functions")
for func in $DOTFILES/functions/*
do
  autoload -Uz ${func:t}
done

for func in $DOTFILES/private/functions/*
do
  autoload -Uz ${func:t}
done

# all of our zsh files
typeset -U _CONF
_CONF=($DOTFILES/**/zsh-config/*.zsh)

# load everything but the path and completion files
for file in ${_CONF:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit promptinit
compinit
promptinit

prompt pure

# load every completion after autocomplete loads
for file in ${(M)_CONF:#*/completion.zsh}
do
  source $file
done

unset _CONF

#######################
#  plugin submodules  #
#######################

source $DOTFILES/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
source $DOTFILES/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source $DOTFILES/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh

zmodload zsh/terminfo
[[ -n "$terminfo[kcuu1]" ]] && bindkey "$terminfo[kcuu1]" history-substring-search-up
[[ -n "$terminfo[cuu1]"  ]] && bindkey "$terminfo[cuu1]"  history-substring-search-up
[[ -n "$terminfo[kcud1]" ]] && bindkey "$terminfo[kcud1]" history-substring-search-down
[[ -n "$terminfo[cud1]"  ]] && bindkey "$terminfo[cud1]"  history-substring-search-down
bindkey "^[[A" history-substring-search-up
bindkey "^[[B"  history-substring-search-down


bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
