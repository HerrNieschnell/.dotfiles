################################################################################
#                                    zshrc                                     #
################################################################################


export DOTFILES=$( cd "${${(%):-%N}:A:h}/.." && pwd )
# your project folder that we can `c [tab]` to
# export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
# all of our zsh files
typeset -U _CONF
_CONF=($DOTFILES/**/zsh-config/*.zsh)

# load the path files
for file in ${(M)_CONF:#*/path.zsh}
do
  source $file
done
export PATH=$DOTFILES/bin:$DOTFILES/private/bin:$PATH

fpath=($fpath "$DOTFILES/functions")
autoload -Uz md extract big-files

# load everything but the path and completion files
for file in ${${_CONF:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit promptinit
compinit
promptinit

prompt pure

# load every completion after autocomplete loads
for file in ${(M)_CONF:#*/completion.zsh}
do
  source $file
done

unset $_CONF

#######################
#  plugin submodules  #
#######################

source $DOTFILES/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
source $DOTFILES/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source $DOTFILES/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh

zmodload zsh/terminfo

bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# vim: set ft=zsh:
